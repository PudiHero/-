%REM
	Agent 系統權限表匯出Excel
	Created 2024/12/19 by ARNO TU/DCT
	Description: Comments for Agent
%END REM
Option Public

Sub Initialize
	Dim s As New NotesSession
	Dim ws As New NotesUIWorkspace
	
	Dim NSDdb As NotesDatabase
	Dim NSDview As NotesView
	Dim tmpdc As NotesDocumentCollection
	Dim NSDdc As NotesDocumentCollection
	Dim NSDdoc As NotesDocument 'dbmap
	
	MsgBox "開始匯出Egr系統權限表Excel"
	
	'選擇存檔路徑
	path = ws.Savefiledialog(False, "請選擇存檔路徑")
	If IsEmpty(path) Then
		MsgBox "未選擇存檔路徑"
		Exit Sub
	End If
	filePath$ = path(0)
	
	'創建Excel物件(電腦需安裝Office)
	Set excelApp = CreateObject("Excel.Application")
	excelApp.Visible = False
	
	Set NSDdb = s.Getdatabase("AP_NOTES/DCT", "DCT\DCT_NSD.nsf") '線上彩虹
	Set NSDview = NSDdb.Getview("DBMAP-UU\By SysCat")
	Set NSDdc = NSDview.Getalldocumentsbykey("EGR", True) '用來創建空dc，避免Merge錯誤
	
	Set tmpdc = NSDview.Getalldocumentsbykey("EGR\存貨", False)
	Call NSDdc.Merge(tmpdc)
	
	Set tmpdc = NSDview.Getalldocumentsbykey("EGR\財務", False)
	Call NSDdc.Merge(tmpdc)
	
	Set tmpdc = NSDview.Getalldocumentsbykey("EGR\財務報表", False)
	Call NSDdc.Merge(tmpdc)
	
	Set tmpdc = NSDview.Getalldocumentsbykey("EGR\會計", False)
	Call NSDdc.Merge(tmpdc)
	
	Set tmpdc = NSDview.Getalldocumentsbykey("Egr\銷售", False)
	Call NSDdc.Merge(tmpdc)
	
	Set NSDdoc = NSDdc.Getfirstdocument
	
	Set workbook = excelApp.Workbooks.Add
	SheetCount% = 1
	While Not NSDdoc Is Nothing
		Set sheet = workbook.Sheets.Add
		'sheet.Name = NSDdoc.DBname(0)
		'Set sheet = workbook.Sheets(SheetCount%)
		
		sheet.Cells(1, 1).Value = "應用系統名稱及版本"
		sheet.Cells(1, 2).Value = NSDdoc.SysCat(0)
		sheet.Cells(2, 1).Value = "用途說明(資料庫說明)"
		sheet.Cells(2, 2).Value = NSDdoc.dbdes(0)
		sheet.Cells(3, 1).Value = "應用系統之作業系統名稱"
		sheet.Cells(3, 2).Value = NSDdoc.OSname(0)
		sheet.Cells(4, 1).Value = "資料庫名稱"
		sheet.Cells(4, 2).Value = NSDdoc.DBname(0)
		sheet.Cells(5, 1).Value = "資料庫主機之作業系統名稱"
		sheet.Cells(5, 2).Value = NSDdoc.DBOSname(0)
		sheet.Cells(6, 1).Value = "系統使用人數"
		sheet.Cells(6, 2).Value = NSDdoc.UserQty(0)
		
		sheet.Cells(1, 3).Value = "系統伺服器所在位置"
		sheet.Cells(1, 4).Value = NSDdoc.Location(0)
		sheet.Cells(2, 3).Value = "應用系統維護方式"
		sheet.Cells(2, 4).Value = NSDdoc.Maintance(0)
		sheet.Cells(3, 3).Value = "應用系統之作業系統版本"
		sheet.Cells(3, 4).Value = NSDdoc.OSver(0)
		sheet.Cells(4, 3).Value = "資料庫版本"
		sheet.Cells(4, 4).Value = NSDdoc.DBver(0)
		sheet.Cells(5, 3).Value = "資料庫主機之作業系統版本"
		sheet.Cells(5, 4).Value = NSDdoc.DBOSver(0)
		
		'(row 7 空白)
		
		sheet.Cells(8, 1).Value = "群組"
		sheet.Cells(8, 2).Value = "存取權"
		sheet.Cells(8, 3).Value = "成員"
		sheet.Cells(8, 4).Value = "類型"
		
		GroupCount% = 0
		For i = 9 To UBound(NSDdoc.Group)+9
			sheet.Cells(i, 1).Value = NSDdoc.Group(GroupCount%)
			sheet.Cells(i, 2).Value = NSDdoc.Level(GroupCount%)
			sheet.Cells(i, 3).Value = NSDdoc.Name(GroupCount%)
			sheet.Cells(i, 4).Value = NSDdoc.UserType(GroupCount%)
			GroupCount% = GroupCount% + 1
		Next
		
		SheetCount% = SheetCount% + 1
		Set NSDdoc = NSDdc.Getnextdocument(NSDdoc)
	Wend
	
	workbook.SaveAs(filePath$+".xlsx")
	
	'關閉Excel
	workbook.Close False
	excelApp.Quit
	
	'釋放記憶體
	Set workbook = Nothing
	Set excelApp = Nothing
	
	MsgBox "Excel匯出成功"
	
End Sub
